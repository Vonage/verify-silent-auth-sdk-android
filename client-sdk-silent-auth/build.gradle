plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.72'
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdkVersion 33
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 3
        versionName "1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
        }
        release {
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        abortOnError false
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = 'com.vonage'
            artifactId = 'client-sdk-silent-auth'
            version = '1.0.2'
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            pom {
                name = 'Vonage Client SDK - Silent Auth'
                description = 'Vonage Verify Silent Authentication uses a mobile phone\'s Subscriber Identity Module (SIM) to prove a user\'s identity, ' +
                        'without any user input. This SDK enables making a HTTP request over cellular even when on WiFi.'
                url = 'https://github.com/Vonage/verify-silent-auth-sdk-android'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'vonage'
                        name = 'Vonage'
                        email = 'devrel@vonage.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/Vonage/verify-silent-auth-sdk-android.git'
                    developerConnection = 'scm:git:ssh://github.com/Vonage/verify-silent-auth-sdk-android.git'
                    url = 'https://github.com/Vonage/verify-silent-auth-sdk-android'
                }

                pom.withXml {
                    // for dependencies and exclusions
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        if(it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null)
                        {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                    def repositoriesNode = asNode().appendNode('repositories')
                    def repositoryNode = repositoriesNode.appendNode('repository')
                    repositoryNode.appendNode('id', 'com.silentauth.sdk')
                    repositoryNode.appendNode('url', 'https://gitlab.com/api/v4/projects/40053021/packages/maven')
                }

                /*dependencies {
                    dependency {
                        groupId = 'com.silentauth.sdk'
                        artifactId = 'silentauth-sdk-android'
                        version = '1.0.5'
                    }
                }
                repositories {
                    repository {
                        id = 'com.silentauth.sdk'
                        url = 'https://gitlab.com/api/v4/projects/40053021/packages/maven'
                    }
                }*/
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = localProperties["nexusUsername"]
                password = localProperties["nexusPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.aar
}

dependencies {
    implementation "com.silentauth.sdk:silentauth-sdk-android:1.0.5"

    implementation "androidx.appcompat:appcompat:$app_compat_version"
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$ktx_version"

    // Testing
    testImplementation "junit:junit:${junit_version}"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1")
}
